import kotlin.Pair
import com.android.build.api.dsl.ApplicationProductFlavor

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"
apply from: "buildConfigs.gradle"
apply plugin: 'idea'

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

ext.setupApplication = { ApplicationProductFlavor flavor, buildConfig ->
    flavor.applicationId buildConfig.applicationId
    flavor.manifestPlaceholders = [*:determineIcon(buildConfig)]

    setupResourceValues("bayern", {
        String type, String name, String value -> flavor.resValue(type, name, value)
    })
}

ext.setupVariant = { variant, buildConfig ->
    if (buildConfig.featureFlags.excludeX86) {
        variant.packaging.jniLibs.excludes.add('**/lib/x86**')
    }
    if (buildConfig.excludeLocationPlayServices) {
        variant.compileConfiguration.exclude group: 'com.google.android.gms', module: 'play-services-location'
        variant.runtimeConfiguration.exclude group: 'com.google.android.gms', module: 'play-services-location'
    }
}

android {
    compileSdkVersion 33

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    flavorDimensions "dummy"
    productFlavors {
        Automatic { flavor ->
            flavor.getIsDefault().set(true)
            // Automatically decides which build config to use based on project properties, dart defines or environment variables.
        }
        Bayern { flavor ->
            // Forces the bayern build config
            setupApplication(flavor, createBuildConfig("bayern"))
        }
        Nuernberg { flavor ->
            setupApplication(flavor, createBuildConfig("nuernberg"))
        }
        BayernFloss { flavor ->
            setupApplication(flavor, createBuildConfig("bayern-floss"))
        }
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    signingConfigs {
        if (localProperties.containsKey("signing.keyAlias")) {
            localProps {
                keyAlias localProperties.getProperty('signing.keyAlias')
                keyPassword localProperties.getProperty('signing.keyPassword')
                storeFile file(localProperties.getProperty('signing.storeFile'))
                storePassword localProperties.getProperty('signing.storePassword')
            }
        }
    }

    buildTypes {
        release {
            if (localProperties.containsKey("signing.keyAlias")) {
                signingConfig signingConfigs.localProps
            } else {
                signingConfig signingConfigs.debug
            }


            // We need to configure proguard here because of some bug which occurred when adding flutter dependencies,
            // probably involving image-picker.
            // The workaround is from https://github.com/flutter/flutter/issues/58479.
            // Also have a look at https://github.com/ehrenamtskarte/ehrenamtskarte/issues/340
            // As of flutter_plugin_android_lifecycle 2.0.1, this should no longer be necessary.
            // Once upgrading to that version, we can remove the next line together with proguard-rules.pro.
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    defaultConfig {
        minSdkVersion 20
        targetSdkVersion 33
        multiDexEnabled true
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
    }

    androidComponents {
        // Verify this using:
        // unzip -l frontend/build/app/outputs/apk/Bayern/debug/app-Bayern-debug.apk | grep x86
        // unzip -l frontend/build/app/outputs/apk/Nuernberg/debug/app-Nuernberg-debug.apk | grep x86
        onVariants(selector().withFlavor(new Pair("dummy", "Bayern")), { variant ->
            setupVariant(variant, createBuildConfig("bayern"))
        })
        onVariants(selector().withFlavor(new Pair("dummy", "Nuernberg")), { variant ->
            setupVariant(variant, createBuildConfig("nuernberg"))
        })
        onVariants(selector().withFlavor(new Pair("dummy", "BayernFloss")), { variant ->
            setupVariant(variant, createBuildConfig("bayern-floss"))
        })
    }
}

flutter {
    source '../..'
    target 'lib/main.dart'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
